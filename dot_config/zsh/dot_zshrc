# -----------------------------------------------------------------------------
# Helper Functions
# -----------------------------------------------------------------------------
eval_if_exists() {
  local cmd="$1"
  shift
  if command -v "$cmd" >/dev/null 2>&1; then
    eval "$("$cmd" "$@")"
  fi
}

source_if_exists() {
  if [[ -f "$1" && -r "$1" ]]; then
    source "$1"
  fi
}

alias_if_exists() {
    local name="$1"
    shift
    local cmd="$*"

    # Extract the actual binary name (first argument only, respecting quotes)
    local bin
    bin=$(printf '%s\n' "$cmd" | awk '{print $1}')

    if command -v "$bin" >/dev/null 2>&1; then
        alias "$name"="$cmd"
    fi
}

# -----------------------------------------------------------------------------
# Evals & Sourcing
# -----------------------------------------------------------------------------
eval_if_exists /opt/homebrew/bin/brew shellenv
eval_if_exists /usr/local/bin/brew shellenv
eval_if_exists pyenv init --path
eval_if_exists pyenv init -
eval_if_exists pyenv virtualenv-init -
eval_if_exists zoxide init zsh
eval_if_exists perl -I"$HOME"/perl5/lib/perl5 -Mlocal::lib="$HOME"/perl5

# fzf integration
if command -v fzf >/dev/null 2>&1; then
  source <(fzf --zsh)
fi

# -----------------------------------------------------------------------------
# Initialize Zsh completion system
# -----------------------------------------------------------------------------
autoload -Uz compinit
compinit -C   # use cache for speed

# -----------------------------------------------------------------------------
# Custom Prompt
# -----------------------------------------------------------------------------
LS_COLORS="di=1;37"
autoload -Uz vcs_info
precmd() { vcs_info }
zstyle ':vcs_info:git:*' formats '(%b) '
zstyle ':vcs_info:*' enable git
setopt PROMPT_SUBST
PROMPT='${CONDA_DEFAULT_ENV:+($CONDA_DEFAULT_ENV) }%1~ ${vcs_info_msg_0_}%% '

# -----------------------------------------------------------------------------
# Sourcing
# -----------------------------------------------------------------------------
brew_share_path="${HOMEBREW_PREFIX:-/opt/homebrew}/share"

source_if_exists "$brew_share_path/zsh-autosuggestions/zsh-autosuggestions.zsh"
source_if_exists "$brew_share_path/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source_if_exists "$brew_share_path/zsh-autopair/autopair.zsh"

source_if_exists "/usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh"
source_if_exists "/usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
source_if_exists "/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source_if_exists "/usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source_if_exists "/usr/share/zsh/plugins/zsh-autopair/zsh-autopair.plugin.zsh"
source_if_exists "/usr/share/zsh-autopair/autopair.zsh"

source_if_exists "$HOME/.config/zsh/themes/soft-light.zsh"
source_if_exists "$HOME/.cache/wal/colors.sh"

# -----------------------------------------------------------------------------
# Global Aliases
# -----------------------------------------------------------------------------
alias_if_exists tree "tree -I 'node_modules|bower_components'"
alias_if_exists la "ls -la"
alias_if_exists l "eza --all --icons"
alias_if_exists cls "clear"
alias_if_exists v "nvim"
alias_if_exists vi "nvim"
alias_if_exists t "tmux a"
alias_if_exists cd "z"
alias_if_exists yy "yazi"
alias_if_exists gs "git status --short"
alias_if_exists fman "compgen -c | fzf | xargs man"
alias_if_exists what "tldr"

# -----------------------------------------------------------------------------
# Script Aliases
# -----------------------------------------------------------------------------
alias_if_exists tt "tmux-new.sh"
alias_if_exists ccp "create-project.sh"
alias_if_exists dp "delete-project.sh"
alias_if_exists m "man.sh"
alias_if_exists bb "branch-change.sh"
alias_if_exists dc "docker-image-runner.sh"
alias_if_exists dd "docker-image-stop.sh"
alias_if_exists ss "stack-search.sh"

# -----------------------------------------------------------------------------
# Conda Initialization (MUST BE LAST)
# -----------------------------------------------------------------------------
conda_paths=(
  "/opt/anaconda3/bin/conda"   # macOS default
  "$HOME/anaconda3/bin/conda"  # Linux default
)

for conda_bin in "${conda_paths[@]}"; do
  if [ -x "$conda_bin" ]; then
    __conda_setup="$("$conda_bin" shell.zsh hook 2> /dev/null)"
    if [ $? -eq 0 ]; then
      eval "$__conda_setup"
    else
      conda_base="${conda_bin%/bin/conda}"
      if [ -f "$conda_base/etc/profile.d/conda.sh" ]; then
        . "$conda_base/etc/profile.d/conda.sh"
      else
        export PATH="$conda_base/bin:$PATH"
      fi
    fi
    unset __conda_setup
    break  # stop after first working conda
  fi
done
